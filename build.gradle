plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'club.aurorapvp'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-releases/"
    }
}

dependencies {
    implementation "com.github.retrooper.packetevents:spigot:2.0.0"

    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    minimize()
    archiveFileName.set("${project.name}-${project.version}.jar")
    relocate 'com.github.retrooper.packetevents', 'club.aurorapvp.fastcrystals.shaded.com.github.retrooper.packetevents'
    relocate 'io.github.retrooper.packetevents', 'club.aurorapvp.fastcrystals.shaded.io.github.retrooper.packetevents'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
